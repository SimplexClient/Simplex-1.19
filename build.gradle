import net.fabricmc.loom.LoomGradleExtension
import org.gradle.internal.os.OperatingSystem

plugins {
	id "fabric-loom" version "0.13-SNAPSHOT"
}

project.ext.lwjglVersion = "3.3.1"

switch (OperatingSystem.current()) {
	case OperatingSystem.LINUX:
		project.ext.lwjglNatives = "natives-linux"
		break
	case OperatingSystem.MAC_OS:
		project.ext.lwjglNatives = "natives-macos"
		break
	case OperatingSystem.WINDOWS:
		project.ext.lwjglNatives = "natives-windows"
		break
}

// Use Java 18 if you prefer
// 17 is the absolute minimum
sourceCompatibility = 17
targetCompatibility = 17

def minecraftVersion = "1.19.2"
def asmVersion = "9.2"
def mainClass = "tk.simplexclient.launch.Main"

version "1.0.0"
group "tk.simplexclient"

repositories {
	maven {
		url "https://repo.spongepowered.org/repository/maven-public/"
	}
	maven {
		url "https://jitpack.io/"
	}
    mavenCentral()
}

for (def runConfig : LoomGradleExtension.get(project).getRunConfigs()) {
	runConfig.defaultMainClass mainClass
}

dependencies {
	minecraft "com.mojang:minecraft:$minecraftVersion"
	mappings loom.officialMojangMappings()
	implementation("com.github.heni123321:LegacyLauncher:ac106bbe00") {
		transitive = false
	}
	implementation "org.lwjgl:lwjgl-nanovg:$lwjglVersion"
	runtimeOnly "org.lwjgl:lwjgl-nanovg:$lwjglVersion:$lwjglNatives"

	annotationProcessor 'org.projectlombok:lombok:1.18.24'

	testCompileOnly 'org.projectlombok:lombok:1.18.24'
	testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'

	compileOnly 'org.projectlombok:lombok:1.18.24'

	implementation "org.spongepowered:mixin:0.8.5"

	implementation "org.ow2.asm:asm:$asmVersion"
	implementation "org.ow2.asm:asm-analysis:$asmVersion"
	implementation "org.ow2.asm:asm-commons:$asmVersion"
	implementation "org.ow2.asm:asm-tree:$asmVersion"
	implementation "org.ow2.asm:asm-util:$asmVersion"

	annotationProcessor "org.ow2.asm:asm:$asmVersion"
	annotationProcessor "org.ow2.asm:asm-analysis:$asmVersion"
	annotationProcessor "org.ow2.asm:asm-commons:$asmVersion"
	annotationProcessor "org.ow2.asm:asm-tree:$asmVersion"
	annotationProcessor "org.ow2.asm:asm-util:$asmVersion"
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}
